buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
    }
}

apply plugin: 'forge'
apply plugin: 'maven-publish'

version = "1.0"
group = "ley.modding.dartcraft"
archivesBaseName = "dartcraft"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
}

repositories {
    mavenLocal()
    maven { url = "https://maven.tilera.xyz" }
    maven {
        url = "https://s3.tilera.xyz/cdn/minecraft/libs/"
        metadataSources { artifact() }
    }
    maven {
        name "central"
        url "https://nexus.covers1624.net/repository/maven-hosted/"
    }
}

dependencies {
    implementation "net.anvilcraft:jalec:0.1.2"
    implementation "net.anvilcraft:anvillib-7:0.1.0:deobf"
    implementation "thaumcraft:Thaumcraft:1.7.10-4.2.3.5:deobf"
    implementation "net.industrial-craft:industrialcraft-2:2.2.827-experimental:dev"
    implementation "codechicken:CodeChickenCore:1.7.10-1.0.7.48:dev"
    implementation "codechicken:CodeChickenLib:1.7.10-1.1.3.141:dev"
    implementation "codechicken:NotEnoughItems:1.7.10-1.0.5.120:dev"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    filesMatching('mcmod.info') {
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

jar {
    manifest {
        attributes "FMLAT": "dartcraft_at.cfg"
    }
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archivesBaseName

            artifact deobfJar
            artifact sourcesJar
            artifact jar
        }
    }

    repositories {
        if (project.hasProperty('mvnURL')) {
                maven {
                    credentials {
                        username findProperty("mvnUsername")
                        password findProperty("mvnPassword")
                    }
                    url = findProperty("mvnURL")
                }
        }
        else {
                mavenLocal()
        }
    }
}
